;P. BERTHAULT 09/23
;avance-version
;from stegp1s
;2D sequence for diffusion measurement using stimulated echo using 1 spoil gradient

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>
define list<gradient> diff=<Difframp>
"DELTA1=d20-p1*2-p30-d16-p19-d16"
"l2=td1/l1"

1 ze
  30m 
2 1m setnmr3|28					; Activate syringe pump and open gas circuit
  d10 							; Push duration
  20u setnmr3^28					; Stop syringe pump and close circuit
4 d1							; Recovery delay
  p0 ph4						; Small flip-angle pulse
  go=4 ph30
  d11 wr #1 if #1 zd
  ; Start of DOSY sequence 
5 1m setnmr3|28					; Activate syringe pump and open gas circuit
  d10 UNBLKGRAMP					; Push duration
  20u setnmr3^28					; Stop syringe pump and close circuit	
  d1								; 
  p1 ph1
  p30:gp6*diff
  d16
  p1 ph2
  d12
  p19:gp7
  d16
  DELTA1
  p1 ph3
  p30:gp6*diff
  d16
  4u BLKGRAMP
  go=5 ph31 
  d11 wr #0 if #0 zd 
  5m igrad diff 
  lo to 2 times l1			; number of points in the indirect dimension of each DOSY
  5m zgrad diff
  lo to 2 times l2			; number of DOSY experiments
exit

ph1= 0 0 0 0 2 2 2 2 1 1 1 1 3 3 3 3
ph2= 0 2
ph3= 1 3 0 2
ph4=0 2
ph30=0 2
ph31=0 0 2 2 2 2 0 0 3 3 1 1 1 1 3 3 

; d16: delay for gradient recovery
; d20: diffusion time (big DELTA)
; td1: number of experiments
; FnMODE: QF
; use xf2 and DOSY processing
; use gradient ratio:    gp 6 : gp 7 100  : -17.13 or else :-)
